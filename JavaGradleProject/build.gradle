plugins {
  id "org.sonarqube" version "2.6.2"
}

apply plugin:"java"
apply plugin:"idea"
apply plugin:"application"
apply plugin:"jacoco"

mainClassName = "adamatti.Main"

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

test {
    maxParallelForks = Runtime.runtime.availableProcessors()
    outputs.upToDateWhen { false }
}

jacoco {
    toolVersion = "0.8.1"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled true
        html.enabled true
    }
}

idea {
    module {
        downloadJavadoc = false
        downloadSources = false

        iml.withXml {
            def node = it.asNode()
            def content = node.component.find { it.'@name' == 'NewModuleRootManager' }.content[0]
            content.sourceFolder.each { sourceFolder ->
                if (sourceFolder.@url?.endsWith('/resources')) {
                    sourceFolder.attributes().with {
                        boolean isTestSource = (remove('isTestSource') == 'true')
                        put('type', isTestSource ? 'java-test-resource' : 'java-resource')
                    }
                }
            }
        }
    }
}

task jenkins (dependsOn: ["test","jacocoTestReport","sonarqube"])